plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.github.node-gradle.node' version '3.5.1'
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation project(':NangPaGo-common')

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch:3.4.0'

    // External Libraries
    implementation 'co.elastic.clients:elasticsearch-java:8.15.4'

    // Firebase
    implementation 'com.google.firebase:firebase-admin:9.2.0'
}

node {
    version = '23.0.0'
    download = true
    nodeProjectDir = file("${project.projectDir}/frontend")
}

tasks.named('processResources') {
    dependsOn copyFrontend
}

tasks.register('npmBuild', NpmTask) {
    dependsOn npmInstall
    args = ['run', 'build']
}

tasks.register('copyFrontend', Copy) {
    dependsOn npmBuild
    from "${project.projectDir}/frontend/dist"
    into "${project.buildDir}/resources/main/static"
}

springBoot {
    buildInfo()
}

bootJar {
    enabled = true
}

jar {
    enabled = false
}
